package require tcltest
package require xbasetcl

namespace import tcltest::*

proc test_path {args} {file nativename [file join [testsDirectory] {*}$args]}
proc testd_setup3 {} {xbase dbf -dbf3 testd}
proc testd_setup4 {} {xbase dbf -dbf4 testd}
proc testd_cleanup {} {
    testd close
    rename testd {}
    foreach x {dbf dbt ndx mdx} {file delete [test_path tmp testd.$x]}
    foreach x {i 0 1 2 3 4 5 6 7 8 9} {file delete [test_path tmp test$x.ndx]}
}

set xbaseLogEnabled [xbase log enabled]
set xbaseLogDirectory [xbase log directory]
set xbaseLogName [xbase log name]
xbase log enabled 0
xbase log directory [file join [testsDirectory] tmp]
xbase log name [file tail [info script]].txt
xbase log enabled 1
xbase log string "START [info script]"

test index-1.0 "index syntax" -setup testd_setup4 -body {
    testd index foo
} -cleanup testd_cleanup -returnCodes 1 -result "bad command \"foo\": must be create, drop, open, close, files, tags, check, reindex, file, tag, info, first, last, prev, next, or find"

test index-1.1.0 "index create syntax" -setup testd_setup4 -body {
    testd index create
} -cleanup testd_cleanup -returnCodes 1 -result "wrong # args: should be \"testd index create ?-ndx|-mdx? ?-filter filter? ?-unique? ?-overlay? ?-descending? expression indexname\""

test index-1.1.1 "index create on unopen dbf" -setup testd_setup4 -body {
    testd index create "F" testi
} -cleanup testd_cleanup -returnCodes 1 -result "Database not open"

test index-1.1.2 "index create simple" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
} -body {
    testd index create "F" testi
} -cleanup testd_cleanup -result [file rootname [test_path tmp testd.dbf]].MDX

test index-1.1.2.ndx "index create simple ndx" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
} -body {
    testd index create -ndx "F" [test_path tmp testi.ndx]
} -cleanup testd_cleanup -result [test_path tmp testi.ndx]

test index-1.1.3 "index create simple check file" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
    testd index create "F" testi
} -body {
   file exists [file rootname [test_path tmp testd.dbf]].MDX
} -cleanup testd_cleanup -result 1

test index-1.1.3.ndx "index create simple check ndx file" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
    testd index create -ndx "F" [test_path tmp testi.ndx]
} -body {
   file exists [test_path tmp testi.ndx]
} -cleanup testd_cleanup -result 1

test index-1.1.3.intl "index create simple intl" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
    testd index create -ndx "F" [test_path tmp тесті.ndx]
} -body {
   file exists [test_path tmp тесті.ndx]
} -cleanup {
   testd_cleanup
   file delete [test_path tmp тесті.ndx]
} -result 1

test index-1.2 "index list" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
    testd index create "F" testi
} -body {
    list [testd index files] [testd index tags]
} -cleanup testd_cleanup -result [list [list [test_path tmp testd.MDX]] [list testi]]

test index-1.2.ndx "index ndx list" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
    testd index create -ndx "F" [test_path tmp testi.ndx]
} -body {
    list [testd index files] [testd index tags]
} -cleanup testd_cleanup -result [list [list [test_path tmp testi.ndx]] [list testi]]

test index-1.3.0 "index drop syntax" -setup testd_setup4 -body {
    testd index drop foo testi
} -cleanup testd_cleanup -returnCodes 1 -result "invalid index type"

test index-1.3.1 "index drop unopen dbf" -setup {
    testd_setup4
} -body {
    testd index drop testi
} -cleanup testd_cleanup -returnCodes {0 1} -result "Database not open"

test index-1.3.2 "index drop unopen ix" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
} -body {
    testd index drop testi
} -cleanup testd_cleanup -returnCodes {0 1} -result "Invalid index tag"

test index-1.3.3 "index drop" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
    testd index create "F" testi
} -body {
   list [testd index drop testi] [file exists [file rootname [test_path tmp testd.dbf]].MDX]
} -cleanup testd_cleanup -result {{} 0}

test index-1.3.3.ndx "index ndx drop" -setup {
    testd_setup4
    testd create {{F N 10 0}} [test_path tmp testd.dbf] testd
    testd index create -ndx "F" [test_path tmp testi.ndx]
} -body {
   list [testd index drop testi] [file exists [test_path tmp testi.ndx]]
} -cleanup testd_cleanup -result {{} 0}

test index-1.4.1 "index autoopen" -setup {
    set f [test_path tmp testd.dbf]
    testd_setup4
    testd create {{F N 10 0}} $f testd
    testd index create "F" testi
    testd close
    rename testd {}
    xbase dbf -dbf4 testd
    testd open $f testd
} -body {
    list [testd index files] [testd index tags]
} -cleanup testd_cleanup -result [list [list [test_path tmp testd.MDX]] [list testi]]

test index-1.4.2 "index close" -setup {
    set f [test_path tmp testd.dbf]
    testd_setup4
    testd create {{F N 10 0}} $f testd
    testd index create "F" testi
    testd close
    rename testd {}
    xbase dbf -dbf4 testd
    testd open $f testd
} -body {
    list \
	[testd index close [test_path tmp testd.MDX]] \
	[testd index files] \
	[testd index tags]
} -cleanup testd_cleanup -result {{} {} {}}

test index-1.4.3 "index close/open" -setup {
    set f [test_path tmp testd.dbf]
    testd_setup4
    testd create {{F N 10 0}} $f testd
    testd index create "F" testi
    testd close
    rename testd {}
    xbase dbf -dbf4 testd
    testd open $f testd
    testd index close [test_path tmp testd.MDX]
} -body {
    list [testd index open [test_path tmp testd.MDX]] [testd index files] [testd index tags]
} -cleanup testd_cleanup -result [list [test_path tmp testd.MDX] [list [test_path tmp testd.MDX]] [list testi]]

test index-1.4.4 "index duplicate open" -setup {
    set f [test_path tmp testd.dbf]
    testd_setup4
    testd create {{F N 10 0}} $f testd
    testd index create "F" testi
    testd close
    rename testd {}
    xbase dbf -dbf4 testd
    testd open $f testd
} -body {
    list [testd index open [test_path tmp testd.MDX]] [testd index files] [testd index tags]
} -cleanup testd_cleanup -result [list [test_path tmp testd.MDX] [list [test_path tmp testd.MDX]] [list testi]]

xbase log string "STOP [info script]"
xbase log enabled 0
xbase log name $xbaseLogName
xbase log directory $xbaseLogDirectory
xbase log enabled $xbaseLogEnabled
