package require tcltest
package require xbasetcl

namespace import tcltest::*

# NOTE: interferation with xbase-5.3
#set xbaseLogEnabled [xbase log enabled]
#set xbaseLogDirectory [xbase log directory]
#set xbaseLogName [xbase log name]
#xbase log enabled 0
#xbase log directory [file join [testsDirectory] tmp]
#xbase log name [file tail [info script]].txt
#xbase log enabled 1
#xbase log string "START [info script]"

test xbase-1.0 "syntax" -body {
    xbase foo
} -returnCodes 1 -result "bad command \"foo\": must be version, dateformat, encoding, expr, list, dbf, open, or log"

test xbase-1.1 "version" {
    xbase version
} {4.1.4}

test xbase-1.2 "date format" {
    set result ""
    lappend result [xbase dateformat]
    lappend result [xbase dateform bla-bla-bla]
    lappend result [xbase date [lindex $result 0]]
} {MM/DD/YY bla-bla-bla MM/DD/YY}

test xbase-1.3 "encoding" {
    set result ""
    lappend result [xbase encoding]
    lappend result [xbase encod ascii]
    lappend result [xbase enc [lindex $result 0]]
} [list [encoding system] ascii [encoding system]]

test xbase-2.1 "dbf parameters error" -body {
    xbase dbf
} -returnCodes 1 -result "wrong # args: should be \"xbase dbf ?-dbf3|-dbf4? name\""

test xbase-2.2 "dbf object" -body {
    xbase dbf testd
} -cleanup {
    rename testd {}
} -result {testd}

test xbase-3.1 "open parameters error" -body {
    xbase open
} -returnCodes 1 -result "wrong # args: should be \"xbase open filename name ?alias?\""

test xbase-3.2 "open file not exists error" -body {
    xbase open notexists testd
} -returnCodes 1 -result "File not found"

test xbase-3.3 "open dbase3 file" -body {
    xbase open [file join [testsDirectory] fixtures dbase_03.dbf] testd
} -cleanup {
    testd close
    rename testd {}
} -result {testd}

#                           excel          cdbf           stackoverflow
# cp1251.dbf                invald ext     Visual FoxPro
# dbase_02.dbf              unknown format ~=
# dbase_03.dbf              open           dBase III      dBase III without memo file
# dbase_8b.dbf              open           dBase IV       dBase IV with memo file
# dbase_8c.dbf              invalid ext    ~=
# dbase_30.dbf              invalid ext    Visual FoxPro  Visual FoxPro
# dbase_31.dbf              invalid ext    Visual FoxPro  Visual FoxPro with AutoIncrement field
# dbase_32.dbf              invalid ext    Visual FoxPro
# dbase_83.dbf              open           dBase III      dBase III with memo file
# dbase_83_missing_memo.dbf open           dBase III
# dbase_f5.dbf              open           FoxPro         FoxPro with memo file
# polygon.dbf               open           Invalid header

foreach {f c r} {
    cp1251.dbf                    1 "Not an Xbase type database"
    dbase_02.dbf                  1 "Not an Xbase type database"
    dbase_03.dbf                  0 ""
    dbase_8b.dbf                  0 ""
    dbase_8c.dbf                  1 "Not an Xbase type database"
    dbase_30.dbf                  1 "Not an Xbase type database"
    dbase_31.dbf                  1 "Not an Xbase type database"
    dbase_32.dbf                  1 "Not an Xbase type database"
    dbase_83.dbf                  0 ""
    dbase_83_missing_memo.dbf     0 ""
    dbase_f5.dbf                  1 "Not an Xbase type database"
    polygon.dbf                   0 ""
} {
    test xbase-3.4.$f "open dbf" -body {
        xbase open [file join [testsDirectory] fixtures $f] testd
        testd close
        rename testd {}
    } -returnCodes $c -result $r
}

test xbase-4.1 "expr syntax" -body {
    xbase expr
} -returnCodes 1 -result "wrong # args: should be \"xbase expr expression\""

test xbase-4.2 "expr invalid syntax" -body {
    xbase expr *
} -returnCodes 1 -result "Parse Error"

foreach {n e r} {
    1 	{2*2} 			"4.0"
    2	{2=2}			"1"
    3	{{01/02/2000}}		"20000102"
    4	{"foo" + "bar"}		"foobar"
} {
    test xbase-4.3.$n "trivial expr" {
        xbase expr $e
    } $r
    unset n e r
}

test xbase-4.4 "db field test" -setup {
    # NOTE: tests hangs on exit when using alias
    # xbase open [file join [testsDirectory] fixtures dbase_03.dbf] testd testd
    xbase open [file join [testsDirectory] fixtures dbase_83.dbf] testd
    testd first
#puts stderr list:[xbase list]
#puts stderr schema:[testd schema]
#puts stderr record:[testd record]
#puts stderr field:[testd field Type]
} -body {
    xbase expr testd->Type
} -cleanup {
    testd close
    rename testd {}
} -result "CMP"

test xbase-5.1 "log syntax" -body {
    xbase log foo
} -returnCodes 1 -result "bad command \"foo\": must be directory, name, size, enabled, string, bytes, or flush"

foreach {o v} {
    directory foo
    name foo
    size 12345
    enabled 1
} {
    set d [xbase log $o]

    test xbase-5.2.$o "log command $o" {
        list [xbase log $o $v] [xbase log $o]
    } [list $v $v]

    xbase log $o $d
    unset o v d
}

test xbase-5.3 "log write/flush" -setup {
    set t [file join [testsDirectory] tmp]
    set e [xbase log enabled]
    set d [xbase log directory]
    set n [xbase log name]
    catch {file delete $t/foo}
    xbase log directory $t
    xbase log name foo
    xbase log enabled true
} -body {
    xbase log string bar
    xbase log flush
    viewFile [xbase log name] [xbase log directory]
} -cleanup {
    xbase log enabled $e
    xbase log directory $d
    xbase log name $n
    catch {file delete $t/foo}
} -result {????-??-?? ??:??:?? - bar} -match glob

#xbase log string "STOP [info script]"
#xbase log enabled 0
#xbase log name $xbaseLogName
#xbase log directory $xbaseLogDirectory
#xbase log enabled $xbaseLogEnabled
